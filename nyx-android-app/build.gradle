// 声明是Android程序，
//com.android.application 表示这是一个应用程序模块
//com.android.library 标识这是一个库模块
//而这区别：前者可以直接运行，后着是依附别的应用程序运行
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.chaquo.python'


android {
    println "android Configuration Lifecycle  invoked"
    // 其实下面这个是一个方法：compileSdkVersion（29）
    // 但是括号可以省略、分号可以省略，就变成下面这个样子了。
    //  /** @see #getCompileSdkVersion() */
    //    public void compileSdkVersion(String version) {
    //        checkWritability();
    //        this.target = version;
    //    }
    compileSdkVersion rootProject.ext.compileSdkVersion    //设置编译时候的Android版本
    //设置编译时使用的构建工具的版本，Android Studio3.0后去除此项配置
    buildToolsVersion rootProject.ext.buildToolsVersion
    // 使用 Databinding必须添加
    dataBinding {
        enabled = true
    }
    defaultConfig {
        // 项目的包名
        applicationId "com.frewen.android.demo"
        // 项目最低兼容的版本
        // 指定项目最低兼容的版本，如果设备小于这个版本或者大于maxSdkVersion(一般不用)将无法安装这个应用，
        // 这里指定为16，表示最低兼容到Android 4.1系统。
        minSdkVersion rootProject.ext.minSdkVersion22
        // 项目的目标版本
        // 指定项目的目标版本，表示在该目标版本上已经做过充分测试，
        // 系统会为该应用启动一些对应该目标系统的最新功能特性，Android系统平台的行为变更，
        // 只有targetSdkVersion的属性值被设置为大于或等于该系统平台的API版本时，才会生效。
        // 例如，若指定targetSdkVersion值为22，则表示该程序最高只在Android5.1版本上做过充分测试，
        // 在Android6.0系统上（对应targetSdkVersion为23）拥有的新特性如系统运行时权限等功能就不会被启用。
        targetSdkVersion rootProject.ext.targetSdkVersion
        // 版本号 一般每次打包上线时该值只能增加，打包后看不见。
        versionCode 1
        // 版本名称 展示在应用市场上的版本名称
        versionName "1.0"
        // 表明要使用AndroidJUnitRunner进行单元测试。
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        python {
            // ......
            pip {
                install "httpstat"
            }
        }
        // 指定abi，如需在模拟器调试，增加"x86"，否则指定"armeabi-v7a"即可
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        // 导入CMake构建
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }
    // 这个闭包主要指定生成安装文件的主要配置，一般包含两个子闭包，一个是debug闭包，用于指定生成测试版安装文件的配置，可以忽略不写；
    // 另一个是release闭包，用于指定生成正式版安装文件的配置。
    // 两者能配置的参数相同，最大的区别默认属性配置不一样，两种模式支持的属性配置如下图：
    buildTypes {
        release {// 生产环境
            minifyEnabled false//是否对代码进行混淆
            //指定混淆的规则文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            pseudoLocalesEnabled false//是否在APK中生成伪语言环境，帮助国际化的东西，一般使用的不多
            zipAlignEnabled true//是否对APK包执行ZIP对齐优化，减小zip体积，增加运行效率
        }
        debug {// 测试环境
            minifyEnabled false//是否对代码进行混淆
            //指定混淆的规则文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug//设置签名信息
            debuggable true     //是否支持断点调试
            jniDebuggable true  //是否可以调试NDK代码
            renderscriptDebuggable false//是否开启渲染脚本就是一些c写的渲染方法
            zipAlignEnabled true//是否对APK包执行ZIP对齐优化，减小zip体积，增加运行效率
            pseudoLocalesEnabled false//是否在APK中生成伪语言环境，帮助国际化的东西，一般使用的不多
            applicationIdSuffix 'debug'//在applicationId 中添加了一个后缀，一般使用的不多
            versionNameSuffix '-debug'//versionName 中添加了一个后缀，一般使用的不多
        }

        externalNativeBuild {
            cmake {
                path "src/main/cpp/CMakeLists.txt"  //构建脚本的路径
                version "3.10.2"                    //CMake的版本
            }
        }
    }

//    productFlavors {
//        google {}
//        xiaomi {}
//        huawei {}
//    }

    applicationVariants.all { variant ->
        variant.outputs.each {

        }
    }

    //使用Java8
    // Butterknife requires Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    repositories {
        flatDir {
            dirs 'libs'   // aar目录
        }
    }
}

dependencies {

    //========添加单元测试相关的依赖================
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    implementation project(path: ':nyx-android-app-library')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation(name: 'httpstat-debug', ext: 'aar')

    implementation 'com.frewen.wang:free-adapter:1.0.1'
    // Kotlin的标准库的依赖
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // ConstraintLayout的依赖
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Dagger2的依赖：https://github.com/google/dagger
    kapt dep.dagger2Compiler
    kapt dep.dagger2AndroidProcessor

    // https://github.com/JakeWharton/RxBinding
    // implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2'


    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
    //eventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    // lombok的最新版本 在3.2上有问题。不能使用，需要降回到1.16
    //  https://github.com/rzwitserloot/lombok/issues/1882
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //    compileOnly 'org.projectlombok:lombok:1.16.22'
    //    annotationProcessor 'org.projectlombok:lombok:1.16.22'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'
    //=========================ButterKnife框架引入===================================================
    kapt dep.butterknifeCompiler


    // 添加JavaWebSocket的依赖
    // https://github.com/TooTallNate/Java-WebSocket
    implementation "org.java-websocket:Java-WebSocket:1.4.0"

    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    annotationProcessor dep.glide
    // 添加材料设计的Material Dialog的依赖
    // https://github.com/afollestad/material-dialogs
    implementation 'com.afollestad.material-dialogs:core:3.1.1'
    implementation 'com.afollestad.material-dialogs:input:3.1.1'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.1.1'


    // <editor-folder desc = "Dependence:JetPack依赖库">
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationVersion"
    //  材料设计的相关依赖
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    // </editor-folder>

    implementation 'devlight.io:navigationtabbar:1.2.5'


    implementation project(':aura-navigation-annotations')
    //如果你使用kotlin,这里需要使用 kapt project
    kapt project(":aura-navigation-compiler")

    // 引入fastjson的依赖：https://github.com/alibaba/fastjson
    implementation 'com.alibaba:fastjson:1.2.70'
    implementation 'com.alibaba:fastjson:1.1.71.android'
    // https://github.com/guolindev/PermissionX
    implementation 'com.permissionx.guolindev:permission-support:1.2.2'
    // https://github.com/hdodenhof/CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // 添加SmartRefreshLayout的开源框架依赖
    implementation 'androidx.appcompat:appcompat:1.0.0'                 //必须 1.0.0 以上
    implementation  'com.scwang.smart:refresh-layout-kernel:2.0.1'      //核心必须依赖
    implementation  'com.scwang.smart:refresh-header-classics:2.0.1'    //经典刷新头
    implementation  'com.scwang.smart:refresh-header-radar:2.0.1'       //雷达刷新头
    implementation  'com.scwang.smart:refresh-header-falsify:2.0.1'     //虚拟刷新头
    implementation  'com.scwang.smart:refresh-header-material:2.0.1'    //谷歌刷新头
    implementation  'com.scwang.smart:refresh-header-two-level:2.0.1'   //二级刷新头
    implementation  'com.scwang.smart:refresh-footer-ball:2.0.1'        //球脉冲加载
    implementation  'com.scwang.smart:refresh-footer-classics:2.0.1'    //经典加载
}


afterEvaluate {
    assembleDebug {
        // dependsOn tasks.findByPath(':helloTaskApi2')
        doFirst {
            println("=================assembleDebug doFirst111111=================")
        }
        doLast {
            println("=================assembleDebug doLast2222222=================")
        }
    }
}

tasks.whenTaskAdded { task ->
    // 通过这个方法我们可以看看Android的构建
    if (task.name == 'assembleDebug') {
        println("=====================:tyche-android-app assembleDebug  whenTaskAdded task:${task.name}=========================")
        println "task  path = ${task.path}"
        println tasks.findByPath(':helloOrderTask3')
        // 这个地方没有生效？？
        // task.mustRunAfter tasks.findByPath(':helloTaskApi2')
        // 如果这个工程目录的Task
        println(tasks.findByPath(":tyche-android-app:assembleDebug"))
        task.doFirst {
            println("=====================assembleDebug  whenTaskAdded doFirst=========================")
        }
        task.doLast {
            println("=====================assembleDebug  whenTaskAdded doLast=========================")
        }
    }
}